%import common.WS_INLINE

// Building blocks

_LINE_END: "$"
_NUMBER_START: "@d"
DIGIT: "1".."5"
DEC_DIGIT: "1".."5" | "a".."d" | "@"

INTEGER: _NUMBER_START DEC_DIGIT+

LOWER_LETTER: "a".."g"
    | "q".."t"
    | "v".."x"
    | "z"
UPPER_LETTER: "A".."G"
    | "Q".."T"
    | "V".."X"
    | "Z"
SAFE_LOWER_LETTER: "b".."c"
    | "e".."g"
    | "q".."r"
    | "v".."x"
    | "z"

SAFE_LETTER: SAFE_LOWER_LETTER | UPPER_LETTER
LETTER: LOWER_LETTER | UPPER_LETTER
CNAME: SAFE_LETTER | LETTER (LETTER | DIGIT)+

IDENTIFIER: CNAME

// Types
// The ! is used to keep track of line numbers for error reporting
!void_type: "v@!d"
!decimal_type: "dect"

?type: void_type | decimal_type

// Expressions
STRING: "`" /.*?/ "`"
?literal: INTEGER | STRING

// Expressions:
expr: literal | IDENTIFIER | exec_expr
exec_expr: "exec" IDENTIFIER expr* "eexec"
var_decl: type IDENTIFIER
params: (var_decl)*
var_decl_stmt: var_decl _LINE_END
expr_stmt: expr _LINE_END

// Statements
ret_stmt: "ret" expr _LINE_END

?statement: ret_stmt | expr_stmt | var_decl_stmt
!block: "%s" (statement)* "%e"
!function_def: "def" type IDENTIFIER params block

start: function_def+

%ignore WS_INLINE